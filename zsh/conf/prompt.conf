## configure prompt

setopt prompt_percent
setopt prompt_subst
setopt transient_rprompt

## at the end, the prompt should look like this:
# $? <user>@<machine> dir [vcs_info] %		${BATTERY}
# battery will be red or green depending on level
# vcs info should be on except for cvs
# maybe rtt-information could be given

autoload -U colors && colors
autoload -Uz vcs_info

blue=%{$fg[blue]%}
red=%{$fg[red]%}
green=%{$fg[green]%}
yellow=%{$fg[yellow]%}
black=%{$fg[black]%}
cyan=%{$fg[cyan]%}
magenta=%{$fg[magenta]%}
white=%{$fg_bold[white]%}
reset_c='%{$reset_color%}'
## function for battery status
export BATTERY=''
get_battery_status()
{
	[[ -z $commands[acpi] ]] && return 1	

	acpiout=$(acpi)
	export BATTERY=${acpiout[(w)4]%,}%
	return 0
}

export MY_RPROMPT='$BATTERY'
RPROMPT="${MY_RPROMPT}"

P_RETURN="%0(?.$green.$red)"'$?'"$reset_c"
P_NAME="%(!.$red.$cyan)%n$reset_c"
P_HOST="${blue}%m$reset_c"
P_DIR="${white}%1~$reset_c"
P_END="%B%#%b"
P_VCS='${vcs_info_msg_0_}'
P_VIM='[${vimode}]'
PROMPT="${P_RETURN} ${P_NAME}%B@${P_HOST}%b ${P_DIR}$P_VCS $P_VIM ${P_END} "


## configure vcs_info via zstyle
# only use for git and svn 
zstyle ':vcs_info:*' enable git svn
zstyle ':vcs_info:*' formats ' %%F{green}[%b]%%f'
zstyle ':vcs_info:*' actionformats ' %%F{green}[%b|%a]%%f'

prepare_prompt() 
{
	get_battery_status
	vcs_info
}

at_precmd+='prepare_prompt'
at_chpwd+='vcs_info'

